using System;
using System.Globalization;
using System.Linq.Expressions;
using System.Reflection;
using System.Threading;

using Nemerle;
using Nemerle.Linq;

namespace LinqToDB_Temp.Common
{
	using Extensions;

	class ConverterMaker
	{
		GetCtor(from : Type, to : Type, p : Expression) : option[Expression]
		{
			def ctor = to.GetConstructor(array[from]);
			if (ctor != null) Some(Expression.New(ctor, array[p])) else None()
		}

		GetValue(from : Type, to : Type, p : Expression) : option[Expression]
		{
			match (from.GetProperty("Value"))
			{
				| null =>
					match (from.GetField("Value"))
					{
						| fi when fi != null && fi.FieldType == to => Some(Expression.Field(p, fi))
						| _ => None()
					}
				| pi when pi.PropertyType == to => Some(Expression.Property(p, pi))
				| _    => None()
			}
		}

		GetOperator(from : Type, to : Type, p : Expression) : option[Expression]
		{
			def op =
				to.GetMethod("op_Implicit", BindingFlags.Static | BindingFlags.Public, null, array[from], null) ??
				to.GetMethod("op_Explicit", BindingFlags.Static | BindingFlags.Public, null, array[from], null);

			if (op != null) Some(Expression.Convert(p, to, op)) else None()
		}

		IsConvertible(type : Type) : bool
		{
			match (Type.GetTypeCode(type))
			{
				| Boolean
				| Byte
				| SByte
				| Int16
				| Int32
				| Int64
				| UInt16
				| UInt32
				| UInt64
				| Single
				| Double
				| Char   => true
				| _      => false
			}
		}

		GetConvertion(from : Type, to : Type, p : Expression) : option[Expression]
		{
			if (IsConvertible(from) && IsConvertible(to) && to != typeof(bool) || from.IsAssignableFrom(to) && to.IsAssignableFrom(from))
				Some(Expression.ConvertChecked(p, to))
			else
				None()
		}

		GetParse(from : Type, to : Type, p : Expression) : option[Expression]
		{
			if (from == typeof(string))
			{
				def mi = to.GetMethod("Parse", BindingFlags.Static | BindingFlags.Public, null, array[from], null);
				if (mi != null) Some(Expression.Convert(p, to, mi)) else None()
			}
			else
				None()
		}

		GetToString(from : Type, to : Type, p : Expression) : option[Expression]
		{
			if (to == typeof(string))
			{
				def mi = from.GetMethod("ToString", BindingFlags.Instance | BindingFlags.Public, null, array[], null);
				if (mi != null) Some(Expression.Call(p, mi)) else None()
			}
			else
				None()
		}

		GetConverter(from : Type, to : Type, p : Expression) : option[Expression]
		{
			if (from == to)
				Some(p)
			else
				GetCtor      (from, to, p) ??
				GetValue     (from, to, p) ??
				GetOperator  (from, to, p) ??
				GetConvertion(from, to, p) ??
				GetParse     (from, to, p) ??
				GetToString  (from, to, p)
		}

		public GetConverter[TFrom,TTo](defaultValueGetter : Type -> object) : Expression[Func[TFrom,TTo]]
		{
			def from = typeof(TFrom);
			def to   = typeof(TTo);

			if (from == to)
				(ToExpression((o : TFrom) => o) : object) :> Expression[Func[TFrom,TTo]]
			else if (to == typeof(object))
				(ToExpression((o : TFrom) => o : object) : object) :> Expression[Func[TFrom,TTo]]
				//(ToExpression(fun(o : TFrom) : object { o }) : object) :> Expression[Func[TFrom,TTo]]
			else
			{
				def p = Expression.Parameter(from, "p");
				def e =
					match (GetConverter(from, to, p))
					{
						| None =>
							def uto   = to.  ToUnderlying();
							def ufrom = from.ToUnderlying();
							def ex    =
								if (from != ufrom)
								{
									def p = Expression.Convert(p, ufrom);

									match (GetConverter(ufrom, to, p))
									{
										| None when to != uto =>
											match (GetConverter(ufrom, uto, p))
											{
												| Some(ex)    => Some(Expression.Convert(ex, to));
												| ex          => ex
											}
										| ex                  => ex
									}
								}
								else
									None();

							match (ex)
							{
								| None when to != uto =>
									match (GetConverter(from, uto, p))
									{
										| Some(ex)    => Some(Expression.Convert(ex, to));
										| ex          => ex
									}
								| ex                  => ex
							}
						| ex   => ex
					};

				def e =
					match (e)
					{
						| Some(ex) when from.IsNullable() with test = Expression.PropertyOrField(p, "HasValue")
						| Some(ex) when from.IsClass      with test = Expression.NotEqual(p, Expression.Constant(null, from)) =>
							Some(Expression.Condition(test, ex, Expression.Constant(defaultValueGetter(to), to)));
						| ex => ex
					}

				match (e)
				{
					| Some(ex) => (Expression.Lambda.[Func[TFrom,TTo]](ex, p) : object) :> Expression[Func[TFrom,TTo]]
					| None     => ToExpression((o : TFrom) => System.Convert.ChangeType(o : object, typeof(TTo), CultureInfo.InvariantCulture) :> TTo)
				}
			}
		}
	}
}
